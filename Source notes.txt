
// This prints the intro text.

10 PRINT TAB(32);"HAMURABI"
20 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
30 PRINT:PRINT:PRINT
80 PRINT "TRY YOUR HAND AT GOVERNING ANCIENT SUMERIA"
90 PRINT "SUCCESSFULLY FOR A TEN-YEAR TERM OF OFFICE.":PRINT

// Init of variables.

95 D1=0:P1=0
// D1 is total starved people.
// P1 is average of people starved / year in percent.

100 Z=0:P=95:S=2800:H=3000:E=H-S
// Z is the current year
// P is the current population
// S is number of stored bushels
// H is harvested bushels
// E is bushels eaten by rats

110 Y=3:A=H/Y:I=5:Q=1
// Y is harvest per acre
// A is acres. acres = total harvest / harvest per acre
// I is imigrants last year.
// Q is here a flag for triggering a plague. Later used for querys.

210 D=0
// People starved last year.

// Print yearly report.

215 PRINT:PRINT:PRINT "HAMURABI:  I BEG TO REPORT TO YOU,":Z=Z+1
217 PRINT "IN YEAR"Z","D"PEOPLE STARVED,"I"CAME TO THE CITY."
// Increase year by 1 and report population changes.

218 P=P+I
// Add imigrants to total population.

// Plague

227 IF Q>0 THEN 230
228 P=INT(P/2)
229 PRINT "A HORRIBLE PLAGUE STRUCK!  HALF THE PEOPLE DIED."
230 PRINT "POPULATION IS NOW"P
// If Q flag is not greater than 0, trigger plague and half population.
// Report population.

232 PRINT "THE CITY NOW OWNS"A"ACRES."
235 PRINT "YOU HARVESTED"Y"BUSHELS PER ACRE."
250 PRINT "RATS ATE"E"BUSHELS."
260 PRINT "YOU NOW HAVE"S"BUSHELS IN STORE.":PRINT
// Report on acres and harvest.

270 IF Z=11 THEN 860
// Check if player has reached the tenth year (11).
// If so, go to win text.

// Buy land.

310 C=INT(10*RND(1)):Y=C+17
// Calculate land price Y (17 to 26)

312 PRINT "LAND IS TRADING AT"Y"BUSHELS PER ACRE."
320 PRINT "HOW MANY ACRES DO YOU WISH TO BUY";
// Print land price and query how much to buy.

321 INPUT Q:IF Q<0 THEN 850
// Store amount to buy in variable Q. If Q is less than 0, go to rejection text and end game.

322 IF Y*Q<=S THEN 330
// If land price times amount to buy is less than stored bushels, The player can afford to buy the land.

323 GOSUB 710
// Else go to not enough bushels sub routine.

324 GOTO 320
// Retry purchase.

330 IF Q=0 THEN 340
// if amount to buy is 0, go to sell land.

331 A=A+Q:S=S-Y*Q:C=0
334 GOTO 400
// Else increase owned land and decrease stored bushels. Set price to 0.
// Skip selling land

// Sell land

340 PRINT "HOW MANY ACRES DO YOU WISH TO SELL";
341 INPUT Q:IF Q<0 THEN 850
// Store amount to sell in variable Q. If Q is less than 0, go to rejection text and end game.

342 IF Q<A THEN 350
// If Q is less than the amount of land owned by the player, go through with the sale.

343 GOSUB 720
// Else go to not enough land sub routine.

344 GOTO 340
Retry sale.

350 A=A-Q:S=S+Y*Q:C=0
// Else decrease owned land and increase stored bushels. Set price to 0.

// Feed people.

400 PRINT
410 PRINT "HOW MANY BUSHELS DO YOU WISH TO FEED YOUR PEOPLE";
411 INPUT Q
412 IF Q<0 THEN 850
// Store amount to feed people in variable Q. If Q is less than 0, go to rejection text and end game.

418 REM *** TRYING TO USE MORE GRAIN THAN IN THE SILOS?
420 IF Q<=S THEN 430
// If Q is less or equal to the amount of bushels stored, go through with the feed order.

421 GOSUB 710
// Else go to not enough bushels sub routine.

422 GOTO 410
// Retry feed order.

430 S=S-Q:C=1:PRINT
// Else decrease stored bushels. Set price to 1.

// Plant

440 PRINT "HOW MANY ACRES DO YOU WISH TO PLANT WITH SEED";
441 INPUT D:IF D=0 THEN 511
// Store amount to plant in D. If D = 0, Set C to random 1 to 6

442 IF D<0 THEN 850
If D is less than 0, go to rejection text and end game.

444 REM *** TRYING TO PLANT MORE ACRES THAN YOU OWN?
445 IF D<=A THEN 450
// If D is less or equal to the amount of owned land, go through with planting.

446 GOSUB 720
// Else go to not enough land sub routine.

447 GOTO 440
// Retry planting.

449 REM *** ENOUGH GRAIN FOR SEED?
450 IF INT(D/2)<S THEN 455
// Use 1/2 bushels per acre. Check if enough in storage. 

452 GOSUB 710
// Else go to not enough bushels sub routine.

453 GOTO 440
// Retry planting.

454 REM *** ENOUGH PEOPLE TO TEND THE CROPS?
455 IF D<10*P THEN 510
// Use 1/10 people per acre. Check if enough population. 

460 PRINT "BUT YOU HAVE ONLY"P"PEOPLE TO TEND THE FIELDS. NOW THEN,"
470 GOTO 440
// Retry planting.

510 S=S-INT(D/2)
// Else decrease stored bushels.

// Harvest.

511 GOSUB 800
// Set C to random 1 to 6

512 REM *** A BOUNTYFULL HARVEST!!
515 Y=C:H=D*Y:E=0
// Set harvest per acre to random value stored in C.
// Set harvest to harvest per acre * number of acres planted.
// Set bushels eaten by rats to 0.

521 GOSUB 800
// Set C to random 1 to 6

522 IF INT(C/2)<>C/2 THEN 530
// Calculate a probability of 50% ?

523 REM *** THE RATS ARE RUNNING WILD!!
525 E=INT(S/C)
// If 50%, calculate bushels eaten by rats and store to E.

530 S=S-E+H
// Add harvest to stored bushels minus bushels eaten by rats.

// Population

531 GOSUB 800
// Set C to random 1 to 6

532 REM *** LET'S HAVE SOME BABIES
533 I=INT(C*(20*A+S)/P/100+1)
// calculate population increase: int(random value C *(20 * acres + stored bushels) / population / 100) + 1

539 REM *** HOW MANY PEOPLE HAD FULL TUMMIES?
540 C=INT(Q/20)
C = number of fed people calculated from bushels as food / 20. Every pop should eat 20 bushels in a year.

541 REM *** HORRORS, A 15% CHANCE OF PLAGUE
542 Q=INT(10*(2*RND(1)-.3))
This calculates a number between -3 and 17. This gives 15% when compared to 0 on line 227

550 IF P<C THEN 210
if there is more fed people than total people, this means no starvation. Run next turn.

551 REM *** STARVE ENOUGH FOR IMPEACHMENT?
552 D=P-C:IF D>.45*P THEN 560
// D is starved people. Calculated from total people - fed people.
// If starved people is greater than 45% of total people. Go to game over.

553 P1=((Z-1)*P1+D*100/P)/Z
// Calculate the averge starved people per year in percent.

555 P=C:D1=D1+D:GOTO 215
// Update population to equal fed people. Add starved people to total of starved people.
// Go to next turn.

// Game over due to starvation.

560 PRINT:PRINT "YOU STARVED"D"PEOPLE IN ONE YEAR!!!"
565 PRINT "DUE TO THIS EXTREME MISMANAGEMENT YOU HAVE NOT ONLY"
566 PRINT "BEEN IMPEACHED AND THROWN OUT OF OFFICE BUT YOU HAVE"
567 PRINT "ALSO BEEN DECLARED 'NATIONAL FINK' !!":GOTO 990

// Subroutine. not enough bushels. 
710 PRINT "HAMURABI:  THINK AGAIN. YOU HAVE ONLY"
711 PRINT S"BUSHELS OF GRAIN. NOW THEN,"
712 RETURN

// Subroutine. not enough land. 
720 PRINT "HAMURABI:  THINK AGAIN. YOU OWN ONLY"A"ACRES. NOW THEN,"
730 RETURN

// Subroutine. Set C to random (1 - 6)
800 C=INT(RND(1)*5)+1
801 RETURN

// Rejection text.

850 PRINT:PRINT "HAMURABI:  I CANNOT DO WHAT YOU WISH."
855 PRINT "GET YOURSELF ANOTHER STEWARD!!!!!"
857 GOTO 990

// Win

860 PRINT "IN YOUR 10-YEAR TERM OF OFFICE,"P1"PERCENT OF THE"
862 PRINT "POPULATION STARVED PER YEAR ON AVERAGE, I.E., A TOTAL OF"
865 PRINT D1"PEOPLE DIED!!":L=A/P
870 PRINT "YOU STARTED WITH 10 ACRES PER PERSON AND ENDED WITH"
875 PRINT L"ACRES PER PERSON.":PRINT
// L is acres per person.

// Different text depending on performance in starvation and acres per person..

880 IF P1>33 THEN 565
885 IF L<7 THEN 565
890 IF P1>10 THEN 940
892 IF L<9 THEN 940
895 IF P1>3 THEN 960
896 IF L<10 THEN 960

900 PRINT "A FANTASTIC PERFORMANCE!!!  CHARLEMANGE, DISRAELI, AND"
905 PRINT "JEFFERSON COMBINED COULD NOT HAVE DONE BETTER!":GOTO 990

940 PRINT "YOUR HEAVY-HANDED PERFORMANCE SMACKS OF NERO AND IVAN IV."
945 PRINT "THE PEOPLE (REMAINING) FIND YOU AN UNPLEASANT RULER, AND,"
950 PRINT "FRANKLY, HATE YOUR GUTS!":GOTO 990

960 PRINT "YOUR PERFORMANCE COULD HAVE BEEN SOMEWHAT BETTER, BUT"
965 PRINT "REALLY WASN'T TOO BAD AT ALL. ";
966 PRINT INT(P*.8*RND(1));"PEOPLE WOULD"
970 PRINT "DEARLY LIKE TO SEE YOU ASSASSINATED BUT WE ALL HAVE OUR"
975 PRINT "TRIVIAL PROBLEMS."

// Ending text

990 PRINT:FOR N=1 TO 10:PRINT CHR;:NEXT N
995 PRINT "SO LONG FOR NOW.":PRINT
999 END